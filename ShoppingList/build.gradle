apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.openintents.shopping"
        versionName "2.0.2"
        versionCode 100202
        minSdkVersion 14
        targetSdkVersion 22

        testApplicationId "org.openintents.shopping.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        play {
            dependencies {
                compile 'com.google.android.gms:play-services-wearable:7.3.0'
                wearApp project(':ShoppingListWear')
            }
        }

        playInternet {
            dependencies {
                compile 'com.google.android.gms:play-services-wearable:7.3.0'
                wearApp project(':ShoppingListWear')
            }
        }

        myo {
            minSdkVersion 16
        }

        tstore {

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }

    def propertyFile = new File(projectDir, "build-private.properties")
    if (propertyFile.exists()) {
        signingConfigs {
            release {
                Properties props = new Properties()
                props.load(new FileInputStream(propertyFile))

                storeFile file(props.getProperty("key.store"))
                storePassword props.getProperty("key.store.password")
                keyAlias props.getProperty("key.alias")
                keyPassword props.getProperty("key.alias.password")
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
        println "Release apks will be signed!"
    } else {
        println "For signed release apks you need to create a build-private.properties file"
    }

}


dependencies {

    repositories {
        maven {
            url '../myorepository'
        }
    }

    compile project(':ShoppingListLibrary')
    compile 'org.openintents:distribution-lib:1.0.2'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile files('libs/sensorsimulator-lib.jar')
    compile files('libs/OIShoppingListMyBackupPro.jar')
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:4.3.1'
    androidTestCompile files('libs/polidea_test_runner_1.2.jar')

    playInternetCompile files('libs/pollfish.jar')
    myoCompile('com.thalmic:myosdk:0.6.+@aar')
}
